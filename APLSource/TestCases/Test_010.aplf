 R←Test_010(stopFlag batchFlag);q;t;all;buffer;⎕TRAP;myLog;TS;noOf;rc;log;source
⍝ Creates a new source in the "Application" log and writes to it.
⍝ Does not only create stuff but also cleans up behind itself.

 R←T._Failed
 ⎕TRAP←(999 'C' '. ⍝ Deliberate error')(0 'N')

 ⎕USING←'System.Diagnostics,System.dll' ''
 log←'Application'
 source←'Test_10_APLTree'

 TS←' '~⍨⍕1↓⎕TS
 q←⎕NEW ##.WindowsEventLog(log source)
 all←''
 q.WriteInfo t←'Server started ',TS
 all,←⊂t
 q.WriteWarning t←'Low on memory ',TS
 all,←⊂t
 q.WriteError t←'Fatal error, server stopped ',TS
 all,←⊂t
 q.WriteInfo t←'Server started ',TS
 all,←⊂t
 noOf←q.NumberOfLogEntries
 buffer←q.ReadThese(noOf⌊100){(⍵-⍺)+(~⎕IO)+⍳⍺}noOf
 →T.GoToTidyUp(⍴all)≠+/∨/¨all⍷¨⊂1↓∊(⎕UCS 13),¨buffer
 R←T._OK

∆TidyUp:
 rc←##.WinReg.DeleteSubKey'HKLM\SYSTEM\CurrentControlSet\Services\EventLog\Application\',source
 ⍎(0≠rc)/'. ??'
